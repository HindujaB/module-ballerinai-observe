apply plugin: 'java'

repositories {
    mavenLocal()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
    }

    maven {
        url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }

    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

configurations {
    jbalTools
    testUtils {
        transitive false
    }
}

dependencies {
    implementation group: 'org.ballerinalang', name: 'ballerina-core', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-runtime', version: "${ballerinaLangVersion}"
    implementation group: 'io.ballerina.stdlib', name: 'observe-native', version: "${stdLibObservabilityVersion}"

    testUtils implementation(group: 'org.ballerinalang', name: 'ballerina-test-utils',
            version: "${ballerinaLangVersion}")
    testImplementation group: 'org.ballerinalang', name: 'ballerina-metrics-extension',
            version: "${ballerinaLangVersion}"
    implementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
}

def ballerinaDist = "build/target/extracted-distributions"

task unpackJballerinaTools(type: Copy) {
    configurations.jbalTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from zipTree(artifact.getFile())
        into new File(ballerinaDist, "jballerina-tools-zip")
    }
}

task unpackStdLibs {
    doLast {
        project.configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            project.copy {
                from project.zipTree(artifact.getFile())
                into new File("${project.buildDir}/target/extracted-distributions", artifact.name + '-zip')
            }
        }
    }
}

task copyStdlibs {
    dependsOn(project.unpackStdLibs)
    doLast {
        /* Standard Libraries */
        project.configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def artifactExtractedPath = "${project.buildDir}/target/extracted-distributions/" + artifact.name + '-zip'
            project.copy {
                def destination = "build/target/extracted-distributions/jballerina-tools-zip/jballerina-tools-${project.ballerinaLangVersion}"
                into destination
                into('repo/bala') {
                    from "${artifactExtractedPath}/bala"
                }
                into('repo/cache') {
                    from "${artifactExtractedPath}/cache"
                }
            }
        }
    }
}

task copyObserveStdlib {
    dependsOn(":testobserve-ballerina:build")
    dependsOn(unpackJballerinaTools)
    dependsOn(copyStdlibs)
    doLast {
        /* Copy Observe module */
        copy {
            from "$project.rootDir/integration-test-utils/ballerina/build/cache_parent/bala"
            into "${ballerinaDist}/jballerina-tools-zip/jballerina-tools-${ballerinaLangVersion}/repo/bala"
        }

        copy {
            from "$project.rootDir/ballerina/build/cache_parent/bala"
            into "${ballerinaDist}/jballerina-tools-zip/jballerina-tools-${ballerinaLangVersion}/repo/bala"
        }
    }
}

task createServerZip(type: Zip) {
    dependsOn copyObserveStdlib

    archiveFileName = "jballerina-tools-${ballerinaLangVersion}.zip"
    destinationDirectory = file("${buildDir}/repacked-distributions")
    from "${project.buildDir}/target/extracted-distributions/jballerina-tools-zip/"
}

test {
    dependsOn createServerZip
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    systemProperty 'enableJBallerinaTests', 'true'
    systemProperty 'basedir', "$buildDir"
    systemProperty 'libdir', "$buildDir"
    systemProperty 'server.zip', createServerZip.outputs.files.singleFile
    systemProperty 'jballerina.server.zip', createServerZip.outputs.files.singleFile
    systemProperty 'java.util.logging.config.file', "$buildDir/resources/test/logging.properties"
    systemProperty 'ballerina.agent.path', configurations.testUtils.asPath
    systemProperty 'ballerina.agent.port.start.value', 27000

    testLogging.showStandardStreams = true

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

build.dependsOn ":integration-test-utils-tests:build"
test.dependsOn ":integration-test-utils-tests:build"